subroutine deortho(ndim,S,C,occ,P)
   implicit none

!! ------------------------------------------------------------------------
!  Input
!! ------------------------------------------------------------------------
   integer, intent(in)    :: ndim               ! number of AOs       
   real(wp),intent(in)    :: C(ndim,ndim)       ! ZDO orbital coefficients
   real(wp),intent(in)    :: occ(ndim)          ! occupations
   real(wp),intent(in)    :: S(ndim*(ndim+1)/2) ! overlap maxtrix in SAO
!! ------------------------------------------------------------------------
!  Output
!! ------------------------------------------------------------------------
   real(wp),intent(out)   :: P(ndim*(ndim+1)/2) ! density matrix

!! ------------------------------------------------------------------------
!  local variables
!! ------------------------------------------------------------------------
   integer  :: i,j,k

   real(wp),allocatable :: si  (:,:)    
   real(wp),allocatable :: espi(:,:)    
   real(wp)             :: xsum         

!! ------------------------------------------------------------------------
!  Lapack work variables
!! ------------------------------------------------------------------------
   real(wp),allocatable :: work(:)
   real(wp),allocatable :: e(:)    
   integer :: lwork, info

   allocate(si(ndim,ndim),espi(ndim,ndim),e(ndim))

   call blowsym(ndim,S,si)
   lwork  = 1 + 6*ndim + 2*ndim**2
   allocate(work(lwork))
   call dsyev ('V','U',ndim,si,ndim,e,work,lwork,info)

   do i=1,ndim 
      do j=1,i
         xsum=0.d0
         do k=1,ndim     
            xsum=xsum+si(k,i)/SQRT(e(k))*si(k,j)        
         enddo
         espi(i,j)=xsum
         espi(j,i)=xsum
      enddo
   enddo

   call DGEMM('N','N',ndim,ndim,ndim,1.0d0,espi,ndim,C,ndim,0.0d0,si,ndim)

   call dmat(ndim,occ,si,P)   

   end
